// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x, b[0..15]=false, sel=zx, out=zxvalue);
    Not16(in=zxvalue, out=notx);
    Mux16(a=zxvalue, b=notx, sel=nx, out=xvalue);

    Mux16(a=y, b[0..15]=false, sel=zy, out=zyvalue);
    Not16(in=zyvalue, out=noty);
    Mux16(a=zyvalue, b=noty, sel=ny, out=yvalue);

    Add16(a=xvalue, b=yvalue, out=xplusy);
    And16(a=xvalue, b=yvalue, out=xandy);
    Mux16(a=xandy, b=xplusy, sel=f, out=xy);
    Not16(in=xy, out=notxy);

    Mux16(a=xy, b=notxy, sel=no, out[0..7]=out1, out[8..15]=out2, out[15]=ng, out=out);
    Or8Way(in=out1, out=orout1);
    Or8Way(in=out2, out=orout2);
    Or(a=orout1, b=orout2, out=orout);
    Not(in=orout, out=zr);
}